plugins {
    id 'java'
    id 'com.github.j2objccontrib.j2objcgradle' version '0.5.0-alpha'
}

j2objcConfig {
    supportedArchs += 'ios_i386'
    xcodeProjectDir '../ios'  // Xcode workspace directory (suggested directory name)
    xcodeTargetsIos 'Mines', 'MinesTests'     // iOS app target name (replace with existing app name)
    translateArgs "--no-package-directories"
    testPattern {
        exclude '*Tests.java'
    }

    finalConfigure()          // Must be last call to configuration
}

sourceCompatibility = 1.7

archivesBaseName = "de.devisnik.mine.model"
version = '1.3.4'

dependencies {
    testCompile 'junit:junit:4.11'
}

jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
    }
}

task sources_jar(type: Jar, dependsOn: classes) {
    description = 'Assembles a jar archive containing the sources.'
    group = 'build'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task link_jars(dependsOn: [jar, sources_jar]) {
    description = 'Creates a properties file conforming Android project standards to link source-jar to code-jar'
    ext.jarFile = new File("${jar.archivePath}")
    ext.jarProperties = new File("${jar.archivePath}.properties")
    ext.sourceJarFile = new File("${sources_jar.archivePath}")
    inputs.file files(jarFile, sourceJarFile)
    outputs.file jarProperties
    doLast {
        file(jarProperties).write "src=${sources_jar.archiveName}\n"
    }
}

artifacts {
    archives sources_jar
    archives(link_jars.jarProperties) {
        type = 'jar.properties' // ensure correct file extension
    }
}

assemble.dependsOn.add('link_jars')

