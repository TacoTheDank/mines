apply plugin: 'com.android.application'
apply plugin: 'android-command'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.getkeepsafe.dexcount'

dependencies {
    implementation project(':android:base')
}

android {
    compileSdkVersion Android.COMPILE_SDK
    buildToolsVersion Android.BUILD_TOOLS
    defaultConfig {
        applicationId Android.APP_NAME
        minSdkVersion Android.MIN_SDK
        targetSdkVersion Android.TARGET_SDK
        versionCode Android.VERSION_CODE
        versionName Android.VERSION_NAME
    }
    signingConfigs {
        release {
            if (rootProject.hasProperty('keystoreFile')) {
                storeFile file(keystoreFile)
                keyAlias keystoreKeyAlias
                storePassword keystoreStorePassword
                keyPassword keystoreKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

if (rootProject.hasProperty('playstoreServiceAccount')) {
    play {
        serviceAccountEmail = playstoreServiceAccount
        pk12File = rootProject.file(playstorePk12File)
        track = 'production'
    }
}

task archiveRelease(type: Copy) {
    from 'build/outputs/apk', 'build/outputs/'
    into "releases/${Android.VERSION_CODE}"
    include('android-release.apk', 'mapping/**')
    rename ('android-release.apk', "${Android.APP_NAME}_${new Date().format('yyyyMMdd')}_${Android.VERSION_NAME}_${Android.VERSION_CODE}.apk")
}
