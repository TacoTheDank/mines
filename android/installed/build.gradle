apply plugin: 'com.android.application'
apply plugin: 'android-command'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.getkeepsafe.dexcount'

sourceCompatibility = 1.7

dependencies {
    implementation project(':android:base')
}

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOLS
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    defaultConfig {
        applicationId APP_NAME
        minSdkVersion MIN_SDK
        targetSdkVersion TARGET_SDK
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }
    signingConfigs {
        release {
            if (rootProject.hasProperty('keystoreFile')) {
                storeFile file(keystoreFile)
                keyAlias keystoreKeyAlias
                storePassword keystoreStorePassword
                keyPassword keystoreKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

if (rootProject.hasProperty('playstoreServiceAccount')) {
    play {
        serviceAccountEmail = playstoreServiceAccount
        pk12File = rootProject.file(playstorePk12File)
        track = 'production'
    }
}

task archiveRelease(type: Copy) {
    from 'build/outputs/apk', 'build/outputs/'
    into "releases/${VERSION_CODE}"
    include('android-release.apk', 'mapping/**')
    rename ('android-release.apk', "${APP_NAME}_${new Date().format('yyyyMMdd')}_${VERSION_NAME}_${VERSION_CODE}.apk")
}
