apply plugin: 'com.android.application'
apply plugin: 'android-command'
apply plugin: 'com.github.triplet.play'

sourceCompatibility = 1.7

dependencies {
    compile project(':model')
}

ext {
    VERSION_CODE = 55
    VERSION_NAME = '1.9.0'
    APP_NAME     = 'de.devisnik.android.mine'
    BUILD_DATE   = today()
}

def today() {
    new Date().format('yyyyMMdd')
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    defaultConfig {
        applicationId APP_NAME
        minSdkVersion 8
        targetSdkVersion 21
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }
    signingConfigs {
        release {
            if (rootProject.hasProperty('keystoreFile'))  {
                storeFile file(keystoreFile)
                keyAlias keystoreKeyAlias
                storePassword keystoreStorePassword
                keyPassword keystoreKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false;
        lintConfig file('lint_config.xml')
    }
}

if (rootProject.hasProperty('playstoreServiceAccount')) {
    play {
        serviceAccountEmail = playstoreServiceAccount
        pk12File = rootProject.file(playstorePk12File)
    }
}

task archiveRelease(type: Copy) {
    from 'build/outputs/apk', 'build/outputs/'
    into "releases/${VERSION_CODE}"
    include('android-release.apk', 'mapping/**')
    rename ('android-release.apk', "${APP_NAME}_${BUILD_DATE}_${VERSION_NAME}_${VERSION_CODE}.apk")
}

