apply plugin: 'com.android.application'

sourceCompatibility = 1.7

dependencies {
    compile project(':model')
}

ext {
    VERSION_CODE = 54
    VERSION_NAME = '1.8.2'
    APP_NAME     = 'de.devisnik.android.mine'
    BUILD_DATE   = today()
}

def today() {
    new Date().format('yyyyMMdd')
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId APP_NAME
        minSdkVersion 8
        targetSdkVersion 18  // below KIT_KAT to ensure measure() is called, as zooming breaks otherwise
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }
    signingConfigs {
        release {
            if (rootProject.hasProperty('keystoreFile'))  {
                storeFile file(keystoreFile)
                keyAlias keystoreKeyAlias
                storePassword keystoreStorePassword
                keyPassword keystoreKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false;
        lintConfig file('lint_config.xml')
    }
}

task archiveRelease(type: Copy) {
    from 'build/outputs/apk', 'build/outputs/'
    into "releases/${VERSION_CODE}"
    include('android-release.apk', 'mapping/**')
    rename ('android-release.apk', "${APP_NAME}_${BUILD_DATE}_${VERSION_NAME}_${VERSION_CODE}.apk")
}
